plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.parcelize'
}

// Read values from gradle.properties or system environment variable
def getWeChatAppId() {
    return readProperty('STRIPE_WECHAT_EXAMPLE_APP_ID')
}

def getPublishableKey() {
    return readProperty('STRIPE_WECHAT_EXAMPLE_STRIPE_KEY')
}

def getBackendUrl() {
    return readProperty('STRIPE_WECHAT_EXAMPLE_BACKEND_URL')
}

private def readProperty(name) {
    final String propValue
    if (hasProperty(name)) {
        propValue = property(name)
    } else {
        propValue = System.getenv(name)
    }

    return propValue?.trim() ? propValue : ""
}

ext {
    retrofitVersion = '2.9.0'
    stripeVersion = '18.2.0'
    lifecycle_version = '2.3.1'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.stripe.android.stripewechatapp"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        manifestPlaceholders = [
                APP_ID: getWeChatAppId(),
                PUBLISHABLE_KEY: getPublishableKey(),
                BACKEND_URL: getBackendUrl()
        ]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation 'androidx.activity:activity-ktx:1.2.4'
    implementation 'com.google.android.material:material:1.4.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.1'


    // main stripe SDK target
    implementation "com.stripe:stripe-android:$stripeVersion"
    // optional wechat pay stripe targets
    implementation "com.stripe:stripe-wechatpay:$stripeVersion"
    // actual wechat pay dependency
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:6.7.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
